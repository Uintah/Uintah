#!/bin/csh
#
#_____________________________________________________________________.
#  This script spits out the variable names for the different components
#   usage:    
#     labelNames <ice/mpmice/arches/mpmarches>
#_____________________________________________________________________.
if ( $#argv == 0) then 
  echo "labelNames <ice/mpmice/arches/mpmarches> "
  exit
endif

set component = $argv

# parce out the build directory 
set numHits = 0
@ counter = 0
while ($numHits != 1 && $counter < 15)
  @ counter ++
  set buildDir = `pwd | cut -d/ -f1-$counter`
  set numHits = `echo $buildDir|grep -c SCIRun`
end  

set componentDir = $buildDir/src/Core/Labels

#__________________________________
# set which label files to look at
if($component == "ice" ) then
  set files = ICELabel.cc
else if( $component == "mpmice" ) then
  set files = (ICELabel.cc MPMICELabel.cc MPMLabel.cc)
else if( $component == "mpm" ) then
  set files = ( MPMLabel.cc)
else if( $component == "arches" ) then
  set componentDir = $buildDir/src/CCA/Components
  set files = ( /Arches/ArchesLabel.cc)
else if( $component == "mpmarches" ) then
  set componentDir = $buildDir/src/CCA/Components
  set files = ( /Arches/ArchesLabel.cc /MPMArches/MPMArchesLabel.cc)
else 
  echo "\ncomponent $component not found, now exiting\n"
  exit
endif

#___________________________________
# find all the varLabels
set labels = ""
foreach file ( $files[*]:q ) 
  echo "-----------------I'm examing $file"
  set labelFile = $componentDir/$file
  grep "create.*" $labelFile| cut -d\" -f2 |grep -v destroy |sort
end
exit
