#!/bin/csh
#__________________________________
#  This script plot the totalMassBurned and its sum
#   output and plot it using gnuplot.
#  Usage:
#     plotBurnedMass <dump postScript File (y/Y), default is no>
#
#  This requires gnuplot 3.8 or greater.
#__________________________________

set out = totalMassBurned.dat
if ( !(-e $out) ) then
    echo "Couldn't find your file $out"
  exit(1)
endif

set hardcopy = "n"
if ( $#argv == 1 ) then
  set hardcopy = $argv
endif 

#__________________________________
echo "computing cumulative sum"
awk 'NR==1 {sum=$2; print $1,$2,sum} ; NR>1 {sum=sum + $2; print $1, $2, sum}' < $out >& $out.tmp
awk 'NR==1 {init_time=$1; print 0} ; NR>1 {time=$1 - init_time; print time, $2, $3}' < $out.tmp > $out.tmp1

/bin/rm $out.tmp

echo "" > gp


#__________________________________
# if making a hardcopy
if( $hardcopy == "y" || $hardcopy == "Y" ) then 
echo "Generating the postscript file plotMassBurned.ps"
/bin/rm gp
cat > gp << fin
set terminal postscript color solid "Times-Roman" 12
set output "plotMassBurned.ps"
fin
endif

#__________________________________
# generate the  plot script
cat >> gp << fin
set grid xtics ytics 
set y2tics

set ylabel 'Mass Burned /timestep  [Kg]' textcolor lt 1
set y2label 'Total Mass Burned [Kg]'     textcolor lt 2
set xlabel 'Physical Time[sec]

plot '$out.tmp1' using 1:2 t           'Mass Burned' with lines,\
     '$out.tmp1' using 1:3 axis x1y2 t 'Cum. Sum' with lines

pause -1 "Hit return to exit"
fin
#__________________________________
# plot it up
gnuplot gp

if($hardcopy == "y" || $hardcopy == "Y") then
  convert plotMassBurned.ps plotMassBurned.jpg
  mogrify -rotate 90 plotMassBurned.jpg
endif

/bin/rm $out.tmp1
exit(1)
