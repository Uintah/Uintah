#!/bin/csh
#
#.......................................................................
# verifyDataTransfer <num Processors> < localDirectory> <login@Machine>:<remote dir>
# This script is uses rsync to verify that all the data transferred with 
# the pscp2 script is all there on the remote machine
# You must be able to ssh onto the remote machine without a password
# or it won't work.
#.......................................................................
#echo $argv
#
#------------------
# unalias commands
unalias ssh
unalias cd
unalias rsync

#------------------
# on purple use gnu commands
set machine = `hostname | tr -d '[0-9]'`
if ( $machine == "up" ) then
  setenv PATH  /usr/local/gnu/bin:$PATH
endif

#------------------
# error checking
if ($#argv != 3) then
  echo ""
  echo " verifyDataTransfer < number of Processors>  < local dir to be copied> <login@Remote Machine>:<remote dir>"
  echo " "
  echo "      This script looks for differences between the local and remote copy of data.  If it detects a difference it"
  echo "      copies the data from the local to remote machine"
  echo ""
  echo " WARNING:"
  echo "      This script assumes that the local data is 'good' and the remote data is 'suspect'"
  echo "      If you've modified the index.xml or some other file on the remote machine it will replace it with the local copy" 
  exit(0)
endif

set nproc         = $argv[1]
set localDir      = $argv[2]
set string        = $argv[3]
set remote_machine = `echo $string |cut -d\@ -f2 | cut -d\: -f1`
set user_test      = `echo $string |cut  -s -d\@ -f1`

if ( $user_test == ""   ) then
  set user_test = $USER
endif

setenv LOGIN              $user_test@$remote_machine
setenv REMOTEDIR_TMP      `echo $string |cut -d\: -f2`
setenv LOCALDIR           $localDir
setenv REMOTEDIR          $REMOTEDIR_TMP/$LOCALDIR/

echo "login:       $LOGIN"
echo "nprocessors: $nproc"
echo "localDir:    $localDir"
echo "remoteDir:   $REMOTEDIR"

#------------------
# Bulletproofing
echo "_____________________________________"
echo "Bullet proof tests"

echo "  Test: does the local folder ($localDir) exist"
if ( ! -e $localDir ) then
  echo " ERROR: local directory $localDir doesn't exist"
  exit(1)
endif

echo "  Test: passwordless access."
set test = `ssh -x $LOGIN "echo 'password_access_works'"`
if ( $test != "password_access_works" ) then
  echo " ERROR:  passwordless access is not working."
  echo "         You must be able to login into the $remote_machine without a password for this script to work"
  exit(1)
endif

echo "  Test: Do we know about the remote operating system"
set OS = `ssh -x $LOGIN uname`
if ( $OS == "IRIX64" ) then
  echo " ERROR: this script only works on Linux machines"
  exit(1)
endif


echo "  Test: does $REMOTEDIR exists on the remote machine"
set test = `ssh -x $LOGIN "if(-e $REMOTEDIR_TMP/$localDir) echo true"`
if ( $test != "true") then
  echo " ERROR:  the remote directory $REMOTEDIR_TMP/$localDir does not exist"
  echo "         Are you sure you typed it in correctly?"
  exit(1)
endif

echo "_____________________________________"
#--------------------------------------------------------------------------
# Now do the work
echo "Now removing stamp directory"
/bin/rm -rf stamp

cd $localDir

#-------------------------------
# make a sorted list of files / directories to test
# let the user edit that file with vi
/bin/rm -f pscp_files
find . -maxdepth 1 -name "*.xml" -exec basename {} \; > pscp_files
find . -maxdepth 1 -name "t*" -exec basename {} \; | sort -r >>pscp_files
find . -maxdepth 1 -name "checkpoints" -exec basename {} \; >> pscp_files

echo "Now starting vi in a separate xterm:  Add/remove the files or directories you want verified to the following list"
xterm -bg "DarkSlateGray" -fg "orange" -e "vi pscp_files" 

/usr/bin/time -p gmake -f /usr/gapps/uintah/scripts/dataVerify_driver -j $nproc

cd ..
exit(1)
