#!/bin/csh
#__________________________________
#  This script pulls out data from sus 
#   output and plots it using gnuplot.
#  Usage:
#     plot_shockTube_1L <uda> <dump postScript File (y/Y), default is no>
#__________________________________

if( $#argv == 0 ) then
  echo "plot_shockTube_1L <sus output file> <dump postScript File (y/Y), default is no>"
  exit(1)
endif
set out = $argv[1]
if ( !(-e $out) ) then
    echo "Couldn't find your uda $out"
  exit(1)
endif

set tmp = (`which lineextract` `which puda`)

if ( $status ) then
  echo " ERROR: Could not find one of the following Uintah utilities"
  echo "    lineextract"
  echo "    puda"
  exit(0)
endif

set hardcopy = "n"
if ( $#argv == 2 ) then
  set hardcopy = $argv[2]
endif 

unalias rm
#__________________________________
# find the timestep to plot
set ts = (`puda -timesteps shockTube.uda | grep : | cut -f 2 -d":"`)

echo "----------------------"
@ count = 0
foreach X ( $ts[*]:q) 
  echo "$count) $X"
  @ count = $count + 1
end 
echo "What timestep would you like to plot?"
set timeStep = $<

#__________________________________
# ExtractData from the data file
lineextract -v temp_CC   -o temp.dat  -l 0 -cellCoords -timestep $timeStep -istart 0 0 0 -iend 10000 0 0 -m 0 -uda $out
lineextract -v vel_CC    -o vel.dat   -l 0 -cellCoords -timestep $timeStep -istart 0 0 0 -iend 10000 0 0 -m 0 -uda $out
lineextract -v press_CC  -o press.dat -l 0 -cellCoords -timestep $timeStep -istart 0 0 0 -iend 10000 0 0 -m 0 -uda $out
lineextract -v rho_CC    -o rho.dat   -l 0 -cellCoords -timestep $timeStep -istart 0 0 0 -iend 10000 0 0 -m 0 -uda $out

inputs/ICE/Scripts/removeBraces vel.dat

cp inputs/ICE/Scripts/riemann.dat $out/exactSolution.dat
mv *.dat $out/.


#__________________________________
# if making a hardcopy
if( $hardcopy == "y" || $hardcopy == "Y" ) then 
echo "Generating the postscript file shockTube.ps"
rm -f gp

cat > gp << fin
set terminal x11 1
set terminal postscript color solid "Times-Roman" 9
set output "shockTube.ps"
fin
endif

#__________________________________
# generate the  plot script
cat >> gp << fin
set ytics
set xtics
set mxtics
set mytics
set grid xtics ytics
set pointsize 1.0
set title ""

#__________________________________
#   Pressure
#__________________________________
set autoscale
set multiplot
set size 0.51,0.51  
set origin 0.0,0.0

set ylabel "Pressure"
set y2tics
set xrange [0:1]
plot  '$out/exactSolution.dat'     using 1:4 t 'exact' with lines, \
      '$out/press.dat'             using 1:4 t ''

#__________________________________
#   Temperature
#__________________________________
set origin 0.5,0.0

set ylabel "Temperature"
plot  '$out/exactSolution.dat'     using 1:5 t 'exact' with lines, \
      '$out/temp.dat'              using 1:4 t ''

#__________________________________
#  velocity x-component
#__________________________________
set origin 0.0,0.5

set ylabel "Velocity"
plot  '$out/exactSolution.dat'     using 1:3 t 'exact'  with lines, \
      '$out/vel.dat'               using 1:4 t ''
#__________________________________
#   density
#__________________________________
set origin 0.5,0.5

set ylabel "Density"
plot  '$out/exactSolution.dat'    using 1:2 t 'exact'  with lines, \
      '$out/rho.dat'              using 1:4 t ''
      
set nomultiplot 
set size 1.0, 1.0
set origin 0.0, 0.0
pause -1 "Hit return to continue"
fin
#__________________________________
# plot it up
gnuplot gp


#__________________________________
# clean up
rm -f gp 
