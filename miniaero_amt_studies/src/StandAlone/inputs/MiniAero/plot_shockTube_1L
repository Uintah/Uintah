#!/bin/csh -f
#______________________________________________________________________
#  This script pulls out data from sus 
#   output and plots it using gnuplot.
#  Usage:
#     plot_shockTube_1L <uda> <dump postScript File (y/Y), default is no>
#______________________________________________________________________


if( $#argv == 0 ) then
  echo "plot_shockTube_1L <uda directory> <dump postScript File (y/Y), default is no>"
  exit(1)
endif
set uda = $argv[1]
if ( !(-e $uda) ) then
    echo "Couldn't find your uda $uda"
  exit(1)
endif

set tmp = (`which lineextract` `which puda`)

if ( $status ) then
  echo " ERROR: Could not find one of the following Uintah utilities"
  echo "    lineextract"
  echo "    puda"
  exit(0)
endif

if( ! -e scripts/ICE/riemann.dat ) then
  echo " ERROR: could not find the exact solution dat file (riemann.dat)"
  echo " Please make sure there is a symbolic link to the inputs directory"
  echo " Try running:"
  echo "      make link_inputs"
  exit
endif

set hardcopy = "n"
if ( $#argv == 2 ) then
  set hardcopy = $argv[2]
endif 

unalias rm
#__________________________________
# find the timestep to plot
set ts = (`puda -timesteps $uda | grep : | cut -f 2 -d":"`)

echo "----------------------"
@ count = 0
foreach X ( $ts[*]:q) 
  echo "$count) $X"
  @ count = $count + 1
end 
echo "What timestep would you like to plot?"
set timeStep = $<

#__________________________________
# ExtractData from the data file
if( -e temp.dat  || -e vel.dat || -e press.dat || -e rho.dat ) then
  /bin/rm -f temp.dat vel.dat press.dat rho.dat
endif
lineextract -v temperature   -o temp.dat  -l 0 -cellCoords -timestep $timeStep -istart -1 0 0 -iend 10000 0 0 -m 0 -uda $uda
lineextract -v velocity      -o vel.dat   -l 0 -cellCoords -timestep $timeStep -istart -1 0 0 -iend 10000 0 0 -m 0 -uda $uda
lineextract -v pressure      -o press.dat -l 0 -cellCoords -timestep $timeStep -istart -1 0 0 -iend 10000 0 0 -m 0 -uda $uda
lineextract -v density       -o rho.dat   -l 0 -cellCoords -timestep $timeStep -istart -1 0 0 -iend 10000 0 0 -m 0 -uda $uda

scripts/removeBraces vel.dat

cp scripts/ICE/riemann.dat exactSolution.dat

echo "set terminal x11 enhanced" > gp
#__________________________________
# if making a hardcopy
if( $hardcopy == "y" || $hardcopy == "Y" ) then 
echo "Generating the postscript file shockTube.ps"
rm -f gp

cat > gp << fin
set terminal postscript color solid "Times-Roman" 12 portrait
set output "shockTube.ps"
fin
endif

#__________________________________
# generate the  plot script
cat >> gp << fin
set ytics
set xtics
set mxtics
set mytics
set grid xtics ytics
set title ""

#__________________________________
#   Pressure
#__________________________________
set autoscale
set multiplot
set size 1,0.25  
set origin 0.0,0.0

set ylabel "Pressure"
set y2tics
#set xrange [0:1]
set pointsize 0.5
plot  'exactSolution.dat'     using 1:4 t 'exact' with lines, \
      'press.dat'             using 1:4 t 'simulation'

#__________________________________
#   Temperature
#__________________________________
set origin 0.0,0.25

set ylabel "Temperature"
plot  'exactSolution.dat'     using 1:5 t 'exact' with lines, \
      'temp.dat'              using 1:4 t 'simulation'

#__________________________________
#  velocity x-component
#__________________________________
set origin 0.0,0.5

set ylabel "Velocity"
plot  'exactSolution.dat'     using 1:3 t 'exact'  with lines, \
      'vel.dat'               using 1:4 t 'simulation'
#__________________________________
#   density
#__________________________________
set origin 0.0,0.75

set ylabel "Density"
plot  'exactSolution.dat'    using 1:2 t 'exact'  with lines, \
      'rho.dat'              using 1:4 t 'simulation'
      
set nomultiplot 
pause -1 "Hit return to continue"
fin
#__________________________________
# plot it up
gnuplot -geometry 900x900 gp

if ( -e shockTube.ps ) then
  ps2pdf shockTube.ps
endif

#__________________________________
# clean up
/bin/rm -f gp temp.dat vel.dat press.dat rho.dat
