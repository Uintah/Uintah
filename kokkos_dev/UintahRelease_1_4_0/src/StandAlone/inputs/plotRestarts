#!/bin/csh
#__________________________________
#  This script pulls out data from sus 
#   output and plots dt and when a timestep was
#   restarted vs elapsedTime.
#  Usage:
#     plotRestarts <sus output file> <dump postScript File (y/Y), default is no>
#__________________________________

if( $#argv == 0 ) then
  echo "plotRestarts <sus output file> <dump postScript File (y/Y), default is no>"
  exit(1)
endif
set out = $argv[1]

if ( !(-e $out) ) then
    echo "Couldn't find your file $out"
  exit(1)
endif 

set hardcopy = "n"
if ( $#argv == 2 ) then
  set hardcopy = $argv[2]
endif 

unalias rm
#__________________________________
# remove your droppings and make a copy of the output file
rm -f gp .data .plotStats_tmp .plotStats_clean
cp $out .plotStats_tmp
grep Time= .plotStats_tmp |grep Mem >& .plotStats_clean

#__________________________________
# extract the data
echo "Now extracting the data"
grep elap .plotStats_clean | cut -d"=" -f4 | cut -d "," -f1 >& .elapsedTime
grep elap .plotStats_clean | cut -d"=" -f2 | cut -d"," -f1 |cut -d" " -f1 >& .physicalTime
grep delT .plotStats_clean | cut -d"=" -f3 | cut -d"," -f1 >& .delT
grep Restarting .plotStats_tmp | cut -d"," -f1 | cut -d" " -f4 >& .restartTime
sdiff .physicalTime .restartTime >& .tmp
sed s/"                                                  <"/""/ .tmp | sed s/"                                                  "/""/>&.tmp2

# remove double restarts per timestep
grep -v ">" .tmp2 >& .tmp3

# paste the temporary files into one data file
paste -d "  " .elapsedTime .delT .tmp3 >& .data

#rm -f .elapsedTime .physicalTime .delT .restartTime .tmp .tmp2 .tmp3 .plotStats_tmp .plotStats_clean
echo "Done extracting data"

echo "" > gp
#__________________________________
# if making a hardcopy
if( $hardcopy == "y" || $hardcopy == "Y" ) then 
echo "Generating the postscript file plotRestarts.ps"
/bin/rm gp
cat > gp << fin
set terminal x11 1
set terminal postscript color solid "Times-Roman" 9
set output "plotRestarts.ps"
fin
endif
 
#__________________________________
# plot it up
cat >> gp << fin
set autoscale
set xdata time
set timefmt "%s"
set grid xtics ytics
set xlabel "ElapsedTime"

plot '.data' using 1:2 t 'dt' with lines,\
     '.data' using 1:( \$4==\$3? \$2 :0  ) t 'restarted'
      
pause -1 "Hit return to exit"
fin
gnuplot gp

#__________________________________
# cleanup
rm -f gp .data .plotStats_tmp
exit(1)
