#!/bin/csh
#
#.......................................................................
# pscp
# pscp <num Processors> < localDirectory> <login@Machine>:<remote dir>
# You must be able to ssh onto the remote machine without a password
# or it won't work.
#.......................................................................
#echo $argv
#
#------------------
# unalias commands
unalias ssh
unalias rm
unalias cd
unalias tar

#------------------
# on purple use gnu commands
set machine = `hostname | tr -d '[0-9]'`
if ( $machine == "up" ) then
  setenv PATH  /usr/local/gnu/bin:$PATH
endif


#------------------
# error checking
if ($#argv != 3) then
  echo " pscp < number of Processors> < local dir to be copied> <login@Remote Machine>:<remote dir>"
  exit(0)
endif

set nproc         = $argv[1]
set localDir      = $argv[2]
set string        = $argv[3]
set remote_machine = `echo $string |cut -d\@ -f2 | cut -d\: -f1`
set user_test      = `echo $string |cut  -s -d\@ -f1`

if ( $user_test == ""   ) then
  set user_test = $USER
endif

setenv LOGIN              $user_test@$remote_machine
setenv REMOTEDIR_TMP      `echo $string |cut -d\: -f2`
setenv LOCALDIR           $localDir
setenv REMOTEDIR          $REMOTEDIR_TMP/$LOCALDIR

echo "login:       $LOGIN"
echo "nprocessors: $nproc"
echo "localDir:    $localDir"
echo "remoteDir:   $REMOTEDIR"

#------------------
# Bulletproofing
echo "Bullet proof tests"

echo "  Test: does the local folder ($localDir) exist"
if ( ! -e $localDir ) then
  echo " ERROR: local directory $localDir doesn't exist"
  exit(1)
endif

echo "  Test: passwordless access."
set test = `ssh -x $LOGIN "echo 'password_access_works'"`
if ( $test != "password_access_works" ) then
  echo " ERROR:  passwordless access is not working."
  echo "         You must be able to login into the $remote_machine without a password for this script to work"
  exit(1)
endif

echo "  Test: Do we know about the remote operating system"
set OS = `ssh -x $LOGIN uname`
if ( $OS != "Linux" && $OS != "IRIX64" ) then
  echo " ERROR: there is no pscp_driver for this operating system: $OS"
  exit(1)
endif
echo "_____________________________________"
#------------------
# Now do the work
echo "Now removing stamp directory"
rm -rf Stamp
echo "Now removing $localDir from $remote_machine if it exists"
ssh -x $LOGIN "cd $REMOTEDIR_TMP; /bin/rm -rf $localDir; mkdir $localDir"

cd $localDir

/usr/bin/time -p gmake TAR=tar -f /usr/gapps/uintah/scripts/pscp_driver_$OS -j $nproc

cd ..
exit(1)
