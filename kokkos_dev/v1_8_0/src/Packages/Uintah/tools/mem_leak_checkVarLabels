#!/bin/csh
#
#_____________________________________________________________________.
#   This script checks to see if all the variables that have
#   been created with Varlabel::create are also destroyed.  This
#   is useful in finding memory leaks.
#   usage;    mem_leak_checkVarLabels <Arches/ICE/MPMICE/MPMLabels.cc>
#
#  NOTE: This script need gnu grep to work.  Simply
#  setenv GNUGREP <path to gnu grep>
#_____________________________________________________________________.

set file = $argv
if ($#argv != 1) then
  echo " mem_leak_checkVarLabels   <fileName> "
  exit(1)
endif

set test = `printenv GNUGREP`
if ( $test == ""  ) then
  echo " you need to set the path to gnu grep"
  echo " setenv GNUGREP <path to gnu grep> "
  exit(1)
endif

set varLabels = (`grep "Label.*=" $file| cut -d= -f1`)
echo "I'm examing the following variables \n "


foreach XXX ( $varLabels[*]:q )
  set has_comment = `echo $XXX | $GNUGREP -c "/"`
  # only check if there isn't a comment in front of the variable
  if ( $has_comment == 0 ) then
    echo $XXX
    set numHits = `$GNUGREP -w -c "$XXX" $file`
    if ( $numHits < 2 ) then
     echo "$XXX ------------------ has been created but hasn't been destroyed"
    endif
  endif
end
