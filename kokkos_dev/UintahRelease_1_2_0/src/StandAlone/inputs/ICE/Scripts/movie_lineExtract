#!/bin/csh
#__________________________________
#  This script pulls out data from sus 
#   output and plots a sequence of timesteps with gnuplot
#  The sequence of movieXXX.ppm files can then be glued together.
#
#  Usage:
#     movie_lineExtract <uda> 
#  Dependencies:
#    imagemagic
#
#__________________________________

if( $#argv == 0 ) then
  echo "movie_lineExtract <sus output file> "
  exit(1)
endif
set uda = $argv[1]
if ( !(-e $uda) ) then
    echo "Couldn't find your uda $uda"
  exit(1) 
endif

set tmp = (`which lineextract` `which puda`)

if ( $status ) then
  echo " ERROR: Could not find one of the following Uintah utilities"
  echo "    lineextract"
  echo "    puda"
  exit(0)
endif

unalias rm
#__________________________________
# find the timestep to plot
set ts = (`puda -timesteps $uda | grep : | cut -f 2 -d":"`)

@ timestep = 0
foreach X ( $ts[*]:q) 

  echo "______________________________________________________________________"
  echo "$timestep) $X"
  
  set fname = `printf 'movie.%04d.ppm\n' $timestep`
  echo $fname
  #__________________________________
  # ExtractData from the data file
  lineextract -v temp_CC   -o temp.dat  -l 0 -cellCoords -timestep $timestep -istart 0 0 0 -iend 100000 0 0 -m 0 -uda $uda
  lineextract -v vel_CC    -o vel.dat   -l 0 -cellCoords -timestep $timestep -istart 0 0 0 -iend 100000 0 0 -m 0 -uda $uda
  lineextract -v press_CC  -o press.dat -l 0 -cellCoords -timestep $timestep -istart 0 0 0 -iend 100000 0 0 -m 0 -uda $uda
  lineextract -v rho_CC    -o rho.dat   -l 0 -cellCoords -timestep $timestep -istart 0 0 0 -iend 100000 0 0 -m 0 -uda $uda

  inputs/ICE/Scripts/removeBraces vel.dat

  mv *.dat $uda/.

  rm -f gp

cat > gp << fin
set terminal x11 1
set terminal postscript color solid "Times-Roman" 18 size 8.88889 in, 6.68 in
set output "plot.ps"

#__________________________________
# generate the  plot script
set ytics
set xtics
set mxtics
set mytics
set grid xtics ytics
set pointsize 1.0
set title ""

#__________________________________
#   Pressure
#__________________________________
set autoscale
set multiplot
set size 0.51,0.51  
set origin 0.0,0.0

set ylabel "Pressure"
set y2tics
plot  '$uda/press.dat'  using 1:4  t '' with linespoints

#__________________________________
#   Temperature
#__________________________________
set origin 0.5,0.0

set ylabel "Temperature"
plot  '$uda/temp.dat'     using 1:4 t '' with linespoints

#__________________________________
#  velocity x-component
#__________________________________
set origin 0.0,0.5

set ylabel "Velocity"
plot  '$uda/vel.dat'      using 1:4 t '' with linespoints
#__________________________________
#   density
#__________________________________
set origin 0.5,0.5

set ylabel "Density"
plot  '$uda/rho.dat'       using 1:4 t '' with linespoints

set nomultiplot 
fin
  #__________________________________
  # plot it up
  gnuplot gp
  
  convert plot.ps $fname
#  mogrify -rotate 90 $fname
#  mogrify -geometry 700x500 $fname

  @ timestep = $timestep + 1
end

#__________________________________
# clean up
rm -f gp 
