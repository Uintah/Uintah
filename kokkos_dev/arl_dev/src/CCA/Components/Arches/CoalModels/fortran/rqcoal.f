	SUBROUTINE rqcoal
C***********************************************************************
C 	RQCOAL CALCULATES ARRAYS OF MEAN ABORPTION AND SCATTERING 
C	EFFICIENCIES FOR COAL AS A FUNCTION OF PARTICLE SIZE AND 
C	TEMPERATURE. SPECTRAL EFFICIENCIES ARE CALCULATED BASED ON 
C	THE SIZE PARAMETER (X) AND RELATIVE REFRACTIVE INDEX (REFREL)
C 	FOR A GIVEN SPHERE REFRACTIVE INDEX, MEDIUM REFRACTIVE INDEX,
C 	RADIUS, AND FREE SPACE WAVELENGTH USING MIE THEORY.
C 	MEAN EFFICIENCIES ARE CALCULATED AS THE AVERAGE OF THE PLANCK
C	AND ROSSELAND MEAN ABSORPTION AND SCATTERING EFFICIENCIES.
C
C	SUBROUTINES USED: RMIE, PLANCK, ROSSEL
C	CODED SEPT 1992 BY BRAD ADAMS.
C***********************************************************************
      PARAMETER (NTD = 19, NTT = 14)
      COMMON
     & /LOGR1/  LRADI,LDBRAD,LCCOEF,LRPART,LRSOOT,LRTURB,LRUSNK
     & /RQMEAN/ QACOAL(NTD,NTT),QSCOAL(NTD,NTT),QAASH(NTD,NTT),
     &          QSASH(NTD,NTT),RTDIA(NTD),RTTEMP(NTT)
     & /RUSNK/ UNCOAL,UKCOAL,UNASH,UKASH
      LOGICAL LRADI,LDBRAD,LCCOEF,LRPART,LRSOOT,LRTURB,LRUSNK
      COMPLEX REFREL,S1(200),S2(200)
      DIMENSION QA(50),QS(50),QE(50),QB(50)
      DATA PI/3.1415927/
C***********************************************************************
C  	REFMED = (REAL) REFRACTIVE INDEX OF SURROUNDING MEDIUM
C  	REFRACTIVE INDEX OF THE SPHERE = REFRE-I*REFIM	 
C  	RADIUS (RAD) AND WAVELENGTH (WAVEL) MUST HAVE THE SAME UNITS!
C	X = SIZE PARAMETER
C	DEFAULT REFRACTIVE INDEX IS FOR BITUMINOUS COAL, IF LRUSNK IS
C	TRUE USER INPUTS ARE USED
C***********************************************************************
	REFMED = 1.0
	IF (LRUSNK) THEN
	  REFRE = UNCOAL
	  REFIM = UKCOAL
	ELSE
	  REFRE = 1.85
	  REFIM = 0.25
	END IF
C***********************************************************************
C	SINCE COAL REFRACTIVE INDEX IS ASSUMED INDEPENDENT OF 
C	TEMPERATURE, TEMPERATURE DEPENDENCY IS ONLY THROUGH PLANCK
C	AND ROSSELAND MEANS, THEREFORE SPECTRAL EFFICIENCIES ONLY NEED
C	TO BE CALCULATED ONCE FOR EACH PARTICLE DIAMETER
C***********************************************************************
	NWVL = 27
	DIAM = 0.50
	DO 300 J=1,NTD
	  RAD = DIAM / 2.
	  WAVEL = 0.250
	  DO 100 I=1,NWVL
   	    REFREL=CMPLX(REFRE,REFIM)/REFMED
   	    X=2.0*PI*RAD*REFMED/WAVEL
C***********************************************************************
C  	NANG = NUMBER OF ANGLES BETWEEN 0 AND 90 DEGREES
C  	MATRIX ELEMENTS CALCULATED AT 2*NANG-1 ANGLES
C  	INCLUDING 0, 90, AND 180 DEGREES
C***********************************************************************
   	    NANG=11
   	    DANG=1.570796327/FLOAT(NANG-1)
	    X = MIN(X,1500.) 
   	    CALL RMIE(X,REFREL,NANG,S1,S2,QEXT,QSCA,QBACK)
	    QA(I) = QEXT - QSCA
	    QS(I) = QSCA
	    IF (I .EQ. 1) WAVEL = WAVEL + 0.250
	    IF (I .GT. 1) WAVEL = WAVEL + 0.500
100	  CONTINUE
C***********************************************************************
C	NOW CALCULATE PLANCK AND ROSSELAND MEAN EFFICIENCIES; 
C	QCOAL'S ARE AVERAGE OF PLANCK & ROSSELAND MEANS
C***********************************************************************
	  TEMP = 250.0
	  DO 200 K=1,NTT
	    CALL PLANCK(TEMP,QA,QAP)
	    CALL PLANCK(TEMP,QS,QSP)
	    CALL ROSSEL(TEMP,QA,QAR)
	    CALL ROSSEL(TEMP,QS,QSR)
	    QACOAL(J,K) = (QAP + QAR) / 2.0
	    QSCOAL(J,K) = (QSP + QSR) / 2.0
	    RTTEMP(K) = TEMP
	    IF (K.LE.9) THEN
	      TEMP = TEMP + 150.0
	    ELSE IF (K.GT.9 .AND. K.LE.11) THEN
	      TEMP = TEMP + 200.0
	    ELSE IF (K.GT.11) THEN
	      TEMP = TEMP + 500.0
	    END IF
200	  CONTINUE
c	write (6,*) 'coal diam =',diam
c	write (6,*) 'qacoal:'
c	write (6,*) (qacoal(j,n),n=1,ntbin)
c	write (6,*) 'qscoal:'
c	write (6,*) (qscoal(j,n),n=1,ntbin)
	  RTDIA(J) = DIAM
	  IF (J.LE.2) THEN
	    DIAM = 2.0 * DIAM
	  ELSE IF (J.GT.2 .AND. J.LE.6) THEN
	    DIAM = DIAM + 2.0
	  ELSE IF (J.GT.6 .AND. J.LE.10) THEN
	    DIAM = DIAM + 2.5
	  ELSE IF (J.GT.10 .AND. J.LE.12) THEN
	    DIAM = DIAM + 5.0
	  ELSE IF (J.GT.12 .AND. J.LE.14) THEN
	    DIAM = DIAM + 10.0
	  ELSE IF (J.GT.14 .AND. J.LE.16) THEN
	    DIAM = DIAM + 25.0
	  ELSE IF (J.GT.16) THEN
	    DIAM = DIAM + 100.0
	  END IF
300	CONTINUE
C
	RETURN
   	END
