#!/bin/csh
#__________________________________
#  This script pulls out data from sus 
#   output and plot it using gnuplot.
#  Usage:
#     plotStat <sus output file> <dump postScript File (y/Y), default is no>
#__________________________________

if( $#argv == 0 ) then
  echo "plotStat <sus output file> <dump postScript File (y/Y), default is no>"
  exit(1)
endif
set out = $argv[1]
if ( !(-e $out) ) then
    echo "Couldn't find your file $out"
  exit(1)
endif

set hardcopy = "n"
if ( $#argv == 2 ) then
  set hardcopy = $argv[2]
endif 

unalias rm
#__________________________________
# remove your droppings and make a copy of the output file
rm -f gp .data .plotStats_tmp
cp $out .plotStats_tmp

#__________________________________
# extract the data
echo "Now extracting the data"

echo "?">&.meanTime
echo "?">>.meanTime  # mean time data isn't collect the first 4 timestps
echo "?">>.meanTime
echo "?">>.meanTime

grep elap .plotStats_tmp | cut -d"=" -f4 | cut -d"," -f1 >& .elapsedTime
grep mean .plotStats_tmp | cut -d":" -f2 | cut -d" " -f2 >> .meanTime
grep elap .plotStats_tmp | cut -d"=" -f2 | cut -d" " -f1 >& .physicalTime
grep delT .plotStats_tmp | cut -d"=" -f3 | cut -d"," -f1 >& .delT

grep delT .plotStats_tmp | cut -d"=" -f5 | cut -d" " -f2 | cut -d"/" -f1 > .memUse1
grep delT .plotStats_tmp | cut -d"=" -f5 | cut -d" " -f2 | cut -d"/" -f2 > .memUse2
grep delT .plotStats_tmp | cut -d"=" -f5 | cut -d" " -f4 | cut -d"/" -f1 > .memUse3
grep delT .plotStats_tmp | cut -d"=" -f5 | cut -d" " -f4 | cut -d"/" -f2 > .memUse4
paste -d "  " .elapsedTime .meanTime .physicalTime .delT .memUse1 .memUse2 .memUse3 .memUse4>& .data

rm -f .elapsedTime .meanTime .physicalTime .delT .memUse1 .memUse2 .memUse3  .memUse4 
rm -f .plotStats_tmp
echo "Done extracting data"

echo "" > gp
#__________________________________
# if making a hardcopy
if( $hardcopy == "y" || $hardcopy == "Y" ) then 
echo "Generating the postscript file plotStats.ps"
/bin/rm gp
cat > gp << fin
set terminal x11 1
set terminal postscript color solid "Times-Roman" 9
set output "plotStat.ps"
fin
endif

#__________________________________
# generate the  plot script
cat >> gp << fin
set multiplot
set size 1.0,0.33 # for three plots
set origin 0.0,0.33
set autoscale
#set xdata time
#set timefmt "%s"             ------- try turning these on
set grid xtics ytics

set y2tics
set ylabel 'Physical Time'
set y2label 'Mean Time/timestep'

plot '.data' using 1:3           t 'Physical Time' with lines,\
     '.data' using 1:2 axes x1y2 t 'meanTime/timestep' with lines
    #----------second plot-------------
set size 1.0,0.33       
set origin 0.0,0.66
set ylabel
set y2label

plot '.data' using 1:4           t 'Delt' with lines, \
     '.data' using 1:3 axes x1y2 t 'Physical Time' with lines

    #----------third plot-------------
set size 1.0,0.33       
set origin 0.0,0.0
set xlabel "ElapsedTime"
set ylabel "Memory useage"
plot '.data' using 1:5 axes x1y2 t 'avg1' with lines, \
     '.data' using 1:6 axes x1y2 t 'avg2' with lines, \
     '.data' using 1:7 axes x1y2 t 'max1' with lines, \
     '.data' using 1:8 axes x1y2 t 'max2' with lines
set nomultiplot
pause -1 "Hit return to exit"
fin
#__________________________________
# plot it up
gnuplot gp


#__________________________________
# clean up
rm -f gp .data

exit(1)
