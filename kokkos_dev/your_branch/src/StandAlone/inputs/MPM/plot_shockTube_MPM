#!/bin/csh
#__________________________________
#  This script pulls out data from sus 
#   output and plots it using gnuplot.
#  Usage:
#     plot_shockTube_1L <uda> <dump postScript File (y/Y), default is no>
#__________________________________

if( $#argv == 0 ) then
  echo "plot_shockTube_1L <sus output file> <dump postScript File (y/Y), default is no>"
  exit(1)
endif
set out = $argv[1]
if ( !(-e $out) ) then
    echo "Couldn't find your uda $out"
  exit(1)
endif

set tmp = (`which puda`)

if ( $status ) then
  echo " ERROR: Could not find one of the following Uintah utilities"
  echo "    puda"
  exit(0)
endif

set hardcopy = "n"
if ( $#argv == 2 ) then
  set hardcopy = $argv[2]
endif 

unalias rm
#__________________________________
# find the timestep to plot
#set ts = (`puda -timesteps shockTube.uda | grep : | cut -f 2 -d":"`)
set ts = (`puda -timesteps $out | grep : | cut -f 2 -d":"`)

echo "----------------------"
@ count = 0
foreach X ( $ts[*]:q) 
  echo "$count) $X"
  @ count = $count + 1
end 
echo "What timestep would you like to plot?"
set timeStep = $<

rm -f partout*

#__________________________________
# ExtractData from the data file
puda -timesteplow $timeStep -timestephigh $timeStep -jim1 -matl 0 $out >& junK
mv partout* $out/partout0
puda -timesteplow $timeStep -timestephigh $timeStep -jim1 -matl 1 $out >& junK
mv partout* $out/partout1

rm -f partout*
rm -f junK

cp scripts/ICE/riemann.dat $out/exactSolution.dat


#__________________________________
# if making a hardcopy
if( $hardcopy == "y" || $hardcopy == "Y" ) then 
echo "Generating the postscript file shockTube.ps"
rm -f gp

cat > gp << fin
set terminal x11 1
set terminal postscript color solid "Times-Roman" 9
set output "shockTube.ps"
fin
endif

#__________________________________
# generate the  plot script
cat >> gp << fin
set ytics
set xtics
set mxtics
set mytics
set grid xtics ytics
set pointsize 1.0
set title ""

#__________________________________
#   Pressure
#__________________________________
set autoscale
set multiplot
set size 0.51,0.51  
set origin 0.0,0.0

set ylabel "Pressure"
set y2tics
set xrange [0:1]
plot  '$out/exactSolution.dat'     using 1:4 t 'exact' with lines, \
      '$out/partout0'              using 1:(-\$4+101325) t '', \
      '$out/partout1'              using 1:(-\$4+101325) t ''

#__________________________________
#   Temperature
#__________________________________
set origin 0.5,0.0

set ylabel "Temperature"
plot  '$out/exactSolution.dat'     using 1:5 t 'exact' with lines, \
      '$out/partout0'              using 1:5 t '', \
      '$out/partout1'              using 1:5 t ''

#__________________________________
#  velocity x-component
#__________________________________
set origin 0.0,0.5

set ylabel "Velocity"
plot  '$out/exactSolution.dat'     using 1:3 t 'exact'  with lines, \
      '$out/partout0'              using 1:2 t '', \
      '$out/partout1'              using 1:2 t ''
#__________________________________
#   density
#__________________________________
set origin 0.5,0.5

set ylabel "Density"
plot  '$out/exactSolution.dat'    using 1:2 t 'exact'  with lines, \
      '$out/partout0'             using 1:3 t '', \
      '$out/partout1'             using 1:3 t ''
      
set nomultiplot 
set size 1.0, 1.0
set origin 0.0, 0.0
pause -1 "Hit return to continue"
fin
#__________________________________
# plot it up
gnuplot gp


#__________________________________
# clean up
rm -f gp 
