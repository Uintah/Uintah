#!/bin/csh
#__________________________________
#  This script pulls out data from sus 
#   output and plot it using gnuplot.
#  Usage:
#     plotStat <sus output file> <dump postScript File (y/Y), default is no>
#
#  This requires gnuplot 3.8 or greater.
#__________________________________

if( $#argv == 0 ) then
  echo "plotStat <sus output file> <dump postScript File (y/Y), default is no>"
  exit(1)
endif
set out = $argv[1]
if ( !(-e $out) ) then
    echo "Couldn't find your file $out"
  exit(1)
endif

set hardcopy = "n"
if ( $#argv == 2 ) then
  set hardcopy = $argv[2]
endif 

unalias rm
#__________________________________
# remove your droppings and make a copy of the output file
rm -f gp .data .plotStats_tmp .plotStats_tmp1 .tmp
cp $out .tmp


grep Time= .tmp |grep Mem >& .plotStats_tmp1

#__________________________________
# extract the data
echo "Now extracting the data"

# When running on frost, the mpirun utility will prepend all text output with "   n:" 
# where n is the processor rank.  This extra colon screws up the grepping below. 
# Check for this pattern of "  0:" and remove it from the .plotStats_tmp file.
sed -e 's/^   0://' .plotStats_tmp1 >& .plotStats_tmp

echo "?">&.meanTime
echo "?">>.meanTime  # mean time data isn't collect the first 4 timestps
echo "?">>.meanTime
echo "?">>.meanTime

grep elap .plotStats_tmp | cut -d"=" -f4 | cut -d"," -f1 >& .elapsedTime
grep mean .plotStats_tmp | cut -d":" -f2 | cut -d" " -f2 >> .meanTime
grep elap .plotStats_tmp | cut -d"=" -f2 | cut -d" " -f1 >& .physicalTime
grep delT .plotStats_tmp | cut -d"=" -f3 | cut -d"," -f1 >& .delT

grep delT .plotStats_tmp | cut -d"=" -f5 | cut -d" " -f2  > .memAve
grep delT .plotStats_tmp | cut -d"=" -f5 | cut -d" " -f4  > .memMax
paste -d "  " .elapsedTime .meanTime .physicalTime .delT .memAve .memMax>& .data

rm -f .elapsedTime .meanTime .physicalTime .delT .memAve .memMax 
rm -f .plotStats_tmp .plotStats_tmp1 .tmp

set procs   = `grep processors $out| cut -d" " -f 2`
set machine = `grep Machine    $out | cut -d":" -f 2`
set date    = `grep -m1 Date   $out | cut -d" " -f2-8,10`

echo "Done extracting data"

echo "" > gp
#__________________________________
# if making a hardcopy
if( $hardcopy == "y" || $hardcopy == "Y" ) then 
echo "Generating the postscript file plotStats.ps"
/bin/rm gp
cat > gp << fin
set terminal x11 1
set terminal postscript color solid "Times-Roman" 9
set output "plotStat.ps"
fin
endif

#__________________________________
# generate the  plot script
cat >> gp << fin

haveStats =0
# does this version have the stats patch 
if ( strstrt(GPVAL_COMPILE_OPTIONS,"+STATS") ) print "Computing the statistics of the mean time per timestep"; haveStats = 1

set multiplot
set size 1.0,0.33 # for three plots
set origin 0.0,0.33
set autoscale
#set xdata time
#set timefmt "%s"             ------- try turning these on
set grid xtics ytics

set y2tics
set ylabel 'Physical Time'       textcolor lt 1
set y2label 'Mean Time/timestep' textcolor lt 2

# compute statistics of mean time and scale the results
if(haveStats == 1) stats '.data' using 2 name "A"; set y2range[A_mean - A_stddev:A_mean + A_stddev]


plot '.data' using 1:3           t 'Physical Time' with lines,\
     '.data' using 1:2 axes x1y2 t 'meanTime/timestep' with lines
    #----------second plot-------------
set size 1.0,0.33       
set origin 0.0,0.66
set title "$date : $machine : $procs Processors"
set ylabel 'Delt'
set y2label 'Physical Time'

plot '.data' using 1:4           t 'Delt' with lines, \
     '.data' using 1:3 axes x1y2 t 'Physical Time' with lines

    #----------third plot-------------
set size 1.0,0.33       
set origin 0.0,0.0
set title ''
set xlabel "ElapsedTime"
set ylabel "Memory useage [MB]"
set y2label ""

plot '.data' using 1:5   t 'ave' with lines, \
     '.data' using 1:6   t 'max' with lines
set nomultiplot
pause -1 "Hit return to exit"
fin
#__________________________________
# plot it up
gnuplot gp


if($hardcopy == "y" || $hardcopy == "Y") then
  echo "converting PostScript files to PDF"
  ps2pdf plotStat.ps
endif

#__________________________________
# clean up
rm -f gp .data

exit(0)
